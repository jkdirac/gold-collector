plugins {
    id 'play'
    id 'idea'
}

def playVersion = '2.4.8'
def scalaVersion = '2.11'

model {
    components {
        play {
            platform play: playVersion, scala: scalaVersion
        }
    }

    distributions {
        playBinary {
            contents {
                from("scripts") {
                    into "bin"
                }
                from("conf/profile/${profile}") {
                    into "conf/profile/target"
                }
            }
        }
    }
}

dependencies {
    play project(':core')
    //play project(':macros')
    play("com.chuusai:shapeless_2.11:2.3.1")
    play("org.scala-lang:scala-compiler:2.11.8")
    play("org.apache.shiro:shiro-core:1.2.4")
    play("org.mindrot:jbcrypt:0.3m")
    play("com.aliyun.oss:aliyun-sdk-oss:2.2.3")
    play("com.aliyun:aliyun-java-sdk-sts:2.1.6")
    play("com.aliyun:aliyun-java-sdk-core:2.1.7")
    play("com.typesafe.play:filters-helpers_$scalaVersion:$playVersion")
}

tasks.withType(PlayRun) {
    forkOptions.jvmArgs = ['-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=9999',
                           "-Dlogback.configurationFile=conf/logback.xml", "-Dconfig.file=conf/profile/${profile}/application.conf"]
}

tasks.withType(Test) {
    jvmArgs = [ "-Dlogback.configurationFile=conf/logback.xml", "-Dconfig.file=conf/profile/${profile}/application.conf"]
}

// repositories added in PlayApp class
repositories {
    jcenter()
    maven {
        name "typesafe-maven-release"
        url "https://repo.typesafe.com/typesafe/maven-releases"
    }
    ivy {
        name "typesafe-ivy-release"
        url "https://repo.typesafe.com/typesafe/ivy-releases"
        layout "ivy"
    }
}
